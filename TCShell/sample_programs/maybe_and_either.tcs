type MaybeNum = number | void

type EitherNumOrString = number | string

type MaybeNumApplyFunctionType = MaybeNum {MaybeNum, MaybeNum {number}}

type EitherNumOrStringApplyFunctionType = EitherNumOrString {EitherNumOrString, EitherNumOrString {number}}

string{} voidError = string(none) {
  return "Error: expected value is void!"
}

MaybeNumApplyFunctionType maybeApplyOperation = MaybeNum (MaybeNum maybe, MaybeNum{number} operation) {
  match maybe {
    number num => return operation(num)
    void e => return (MaybeNum) none
  }
}

EitherNumOrStringApplyFunctionType eitherApplyOperationOrReturnAndLogError = EitherNumOrString(EitherNumOrString either,
  EitherNumOrString{number} operation) {
  match either {
    number num => return operation(num)
    string str => {
      print(str)
      return (EitherNumOrString) str
    }
  }
}

MaybeNum{number} returnsOddNumbers = MaybeNum(number x) {
  match x % 2 {
    1 => return (MaybeNum) x
    0 => return (MaybeNum) none
  }
}

EitherNumOrString{MaybeNum} parseMaybeNumOrReturnError = EitherNumOrString(MaybeNum maybe) {
  match maybe {
    number num => return (EitherNumOrString) num
    void e => return (EitherNumOrString) voidError()
  }
}

EitherNumOrString{number} printAndReturnOddNumber = EitherNumOrString(number oddNumber) {
  print("The following is Odd:")
  print(oddNumber)
  return (EitherNumOrString) oddNumber
}

number sum = 0

void{} Main = void(none) {
  EitherNumOrString{number} addToSum = EitherNumOrString(number x) {
    sum = sum + x
    return (EitherNumOrString) sum
  }
  number iter = 0
  while iter < 16 {
    MaybeNum maybeOdd = (MaybeNum) iter
    maybeOdd = maybeApplyOperation(maybeOdd, returnsOddNumbers)
    EitherNumOrString numOrError = parseMaybeNumOrReturnError(maybeOdd)
    numOrError = eitherApplyOperationOrReturnAndLogError(numOrError, printAndReturnOddNumber)
    eitherApplyOperationOrReturnAndLogError(numOrError, addToSum)
    iter = iter + 1
  }
  print("The sum of all odd numbers from 0-15:")
  print(sum)
}

Main()
