union MaybeNum = number | void

union EitherNumOrString = number | string

string() voidError = string(none) {
  return "Error: expected value is void!"
}

union MaybeNum(union MaybeNum, union MaybeNum(number)) maybeApplyOperation = union MaybeNum(union MaybeNum maybe, union MaybeNum(number) operation) {
  match maybe {
    number num => return operation(num)
    void e => return (union MaybeNum) none
  }
}

union EitherNumOrString(union EitherNumOrString, union EitherNumOrString(number)) eitherApplyOperationOrReturnAndLogError = union EitherNumOrString(union EitherNumOrString either, union EitherNumOrString(number) operation) {
  match either {
    number num => return operation(num)
    string str => {
      print(str)
      return (union EitherNumOrString) str
    }
  }
}

union MaybeNum(number) returnsOddNumbers = union MaybeNum(number x) {
  match x % 2 {
    1 => return (union MaybeNum) x
    0 => return (union MaybeNum) none
  }
}

union EitherNumOrString(union MaybeNum) parseMaybeNumOrReturnError = union EitherNumOrString(union MaybeNum maybe) {
  match maybe {
    number num => return (union EitherNumOrString) num
    void e => return (union EitherNumOrString) voidError()
  }
}

union EitherNumOrString(number) printAndReturnOddNumber = union EitherNumOrString(number oddNumber) {
  print("The following is Odd:")
  print(oddNumber)
  return (union EitherNumOrString) oddNumber
}

number sum = 0

void() Main = void(none) {
  union EitherNumOrString(number) addToSum = union EitherNumOrString(number x) {
    sum = sum + x
    return (union EitherNumOrString) sum
  }
  number iter = 0
  while iter < 16 {
    union MaybeNum maybeOdd = (union MaybeNum) iter
    maybeOdd = maybeApplyOperation(maybeOdd, returnsOddNumbers)
    union EitherNumOrString numOrError = parseMaybeNumOrReturnError(maybeOdd)
    numOrError = eitherApplyOperationOrReturnAndLogError(numOrError, printAndReturnOddNumber)
    eitherApplyOperationOrReturnAndLogError(numOrError, addToSum)
    iter = iter + 1
  }
  print("The sum of all odd numbers from 0-15:")
  print(sum)
}

Main()
